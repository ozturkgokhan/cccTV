import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.oss.licenses.plugin'

ext {
    androidKtx = "0.3"
    archComponentsVersion = "1.0.0"
    archComponentsLifecycleVersion = "1.1.0"
    assertjVersion = "3.8.0"
    c3mediaVersion = "1.1.0"
    crashlyticsVersion = "2.9.0"
    daggerVersion = "2.14.1"
    exoplayerVersion = "2.7.0"
    glideVersion = "4.6.1"
    gsonVersion = "2.8.2"
    playServicesVersion = "11.4.0"
    rxAndroidVersion = "2.0.2"
    rxBindingVersion = "2.1.1"
    rxJavaVersion = "2.1.10"
    rxKotlinVersion = "2.2.0"
    supportLibVersion = "27.1.1"
    timberVersion = "4.6.1"

    // Test dependencies
    junitVersion = "4.12"
    kluentVersion = "1.34"
    mockitoVersion = "2.8.47"
    runnerVersion = '1.0.1'
    rulesVersion = '1.0.1'
}

android {

    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "de.stefanmedack.ccctv"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 13
        versionName "3.0.0"
        resConfigs "en", "de"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(cccTvKeyStore)
            storePassword cccTvStorePassword
            keyAlias cccTvKeyAlias
            keyPassword cccTvKeyPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-TV-D-${getFormattedDate()}"
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            versionNameSuffix '-TV'
            // TODO proguard is your friend
//            minifyEnabled true
//            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "streaming"
    productFlavors {
        devStreaming {
            buildConfigField "String", "STREAMING_API_BASE_URL", "\"http://gist.githubusercontent.com\""
            buildConfigField "String", "STREAMING_API_OFFERS_PATH",
                    "\"/MaZderMind/a91f242efb2f446a2237d4596896efd6/raw/7ab4e206f19ed4d63a67917fe6e3a15a96218ac9/streams-v2.json\""
//                    "\"/johnjohndoe/617bbfa2ac36f5148a049548b419e299/raw/7ab4e206f19ed4d63a67917fe6e3a15a96218ac9/streams-v2.json\""
        }
        liveStreaming {
            buildConfigField "String", "STREAMING_API_BASE_URL", "\"http://streaming.media.ccc.de\""
            buildConfigField "String", "STREAMING_API_OFFERS_PATH", "\"/streams/v2.json\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Support Libs
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:leanback-v17:$supportLibVersion"

    // Architecture Components - View Model (+ Lifecycles, LiveData)
    implementation "android.arch.lifecycle:runtime:$archComponentsLifecycleVersion"
    implementation "android.arch.lifecycle:extensions:$archComponentsLifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$archComponentsLifecycleVersion"

    // Architecture Components - Room
    implementation "android.arch.persistence.room:runtime:$archComponentsVersion"
    implementation "android.arch.persistence.room:rxjava2:$archComponentsVersion"
    kapt "android.arch.persistence.room:compiler:$archComponentsVersion"

    implementation "androidx.core:core-ktx:$androidKtx"

    //Dagger 2 dependencies
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // c3media library TODO update import after release
//    implementation "info.metadude.kotlin.library.c3media:c3media-rx-java-2:$c3mediaVersion"
    implementation "com.github.stefanmedack.c3media-base:c3media-rx-java-2:api_update_beta1"
    // c3media streaming
    implementation "com.github.johnjohndoe:Brockman:1886e90a92029ceba978e3326202383c63ed8efd"

    // Exo Player
    implementation "com.google.android.exoplayer:exoplayer:$exoplayerVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // RXJava
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // RXBinding
    implementation "com.jakewharton.rxbinding2:rxbinding-leanback-v17-kotlin:$rxBindingVersion"

    // timber - better logging
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // Licenses Screen
    implementation "com.google.android.gms:play-services-oss-licenses:$playServicesVersion"

    // Test dependencies
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "org.amshove.kluent:kluent:$kluentVersion"

    // Android Instrumented Test
    androidTestImplementation "com.android.support.test:runner:$runnerVersion"
    androidTestImplementation "org.amshove.kluent:kluent-android:$kluentVersion"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // Arch Component
    androidTestImplementation "android.arch.core:core-testing:$archComponentsVersion"

}

static def getFormattedDate() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'GMT+2'")
    df.setTimeZone(TimeZone.getTimeZone("GMT+2")) // Berlin Timezone
    return df.format(new Date())
}